apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"

def adMobPropertiesFile = rootProject.file("ad-mob.properties")
def adMobProperties = new Properties()
adMobProperties.load(new FileInputStream(adMobPropertiesFile))

android {
    namespace = "com.arthurnagy.staysafe"
    compileSdk = 34

    defaultConfig {
        applicationId "com.arthurnagy.staysafe"
        minSdkVersion 26
        targetSdkVersion 34
        versionCode 16
        versionName "1.8.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true

        buildConfigField("String", "AD_MOB_APP_ID", "\"${adMobProperties['appId']}\"")
        manifestPlaceholders = [adMobAppId: "${adMobProperties['appId']}"]

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental"   : "true"
//                        "room.expandProjection":"true"
                ]
            }
        }
    }

    buildTypes {
        debug {
            buildConfigField("String", "AD_MOB_BANNER_UNIT_ID", "\"ca-app-pub-3940256099942544/6300978111\"")
        }
        release {
            buildConfigField("String", "AD_MOB_BANNER_UNIT_ID", "\"${adMobProperties['bannerUnitId']}\"")

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        viewBinding true
        dataBinding true
        buildConfig true
    }
     compileOptions {
         sourceCompatibility JavaVersion.VERSION_17
         targetCompatibility JavaVersion.VERSION_17
     }
     kotlinOptions {
         jvmTarget = "17"
         freeCompilerArgs += "-Xuse-experimental=kotlin.Experimental"
     }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(":signature-pad")

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation 'androidx.core:core-ktx:1.13.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation 'com.google.android.material:material:1.11.0'
    implementation "com.tbuonomo:dotsindicator:5.0"

    implementation "dev.chrisbanes.insetter:insetter-dbx:0.6.1"

    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    implementation(platform("io.insert-koin:koin-bom:3.5.6"))
    implementation("io.insert-koin:koin-android")
    implementation("io.insert-koin:koin-androidx-navigation")

    implementation 'androidx.browser:browser:1.8.0'
    implementation 'androidx.webkit:webkit:1.10.0'

    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation 'io.coil-kt:coil:2.5.0'

    implementation "com.android.billingclient:billing:$billing"
    implementation "com.android.billingclient:billing-ktx:$billing"

    implementation 'com.google.android.gms:play-services-ads:23.0.0'

    implementation 'com.jakewharton.timber:timber:5.0.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

}